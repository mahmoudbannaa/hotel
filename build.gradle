  plugins {
  id 'java'
  id 'org.springframework.boot' version '1.5.6.RELEASE'
  id 'eclipse'
  id 'jacoco'
  id 'checkstyle'
  id 'pmd'
  id 'project-report'
  id 'build-dashboard'
}

ext {
  jacocoVersion = '0.7.7.201606060606'
  checkstyleVersion = '7.2'
  pmdVersion = '5.5.2'
  guavaVersion = '20.0'
  swaggerVersion = '2.6.1'
}

group = 'com.expedia'
version = '1.0.0'
sourceCompatibility = 1.8

repositories {
  jcenter()
}

dependencies {
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  compile group: 'org.springframework', name: 'spring-context-support'

  compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-joda'
  compile group: 'io.springfox', name: 'springfox-swagger2', version: swaggerVersion
  compile group: 'io.springfox', name: 'springfox-swagger-ui', version: swaggerVersion

  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
}

defaultTasks 'clean', 'build'

springBoot {
  buildInfo()
}

jacoco {
  toolVersion = jacocoVersion
}

jacocoTestReport {
  reports {
    html.enabled = true
    xml.enabled = true
    csv.enabled = true
  }
}
test.finalizedBy jacocoTestReport

checkstyle {
  toolVersion = checkstyleVersion
  config = rootProject.resources.text.fromFile('etc/checkstyle/rules.xml')
}

pmd {
  toolVersion = pmdVersion
  ruleSetConfig = rootProject.resources.text.fromFile('etc/pmd/ruleset.xml')
  ignoreFailures = true
}

check.finalizedBy projectReport

projectReport.finalizedBy buildDashboard
